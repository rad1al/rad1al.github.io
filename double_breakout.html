<html>
<head>
    <title>double_breakout</title>
    <script src="processing-1.4.8.min.js"></script>
</head>
<body>
    <center><h1>double_breakout</h1>
    <p>Sketch rendered with Processing.js.</p>
    <script type="text/processing" data-processing-target="processing-canvas">
    /**  
    * A Simple Solar System Animation
    * @author Charles Jin
    * @version 1.0 
    * Copyright 2017
    */ 
    
    float screen_width = 1200;
    float screen_height = 1000;
    
    ArrayList<Brick> bricks = new ArrayList<Brick>(); 
    boolean keyA = false;
    boolean keyD = false;
    boolean keyR = false;
    
    
    Ball ball = new Ball(80, -300);
    Paddle paddle_lower = new Paddle(-65, -screen_height/2+20, 10); 
    Paddle paddle_upper = new Paddle(-65, screen_height/2-20, -10); 
    Stage stage = new Stage(screen_width, screen_height);
    
    
    void setup() {
      size(1200, 1000); // Set window dimensions.
      pixelDensity(2); // Turn on retina mode for retina screens. Doesn't work with Processing.js
      create_bricks();
    }
    
    void draw() {
      background(#000000);
      fill(#FFFFFF);
      ball.move();
      paddle_lower._draw();
      paddle_upper._draw();
      draw_bricks();
      paddle_lower.move();
      paddle_upper.move();
      stage.draw_score();
      ball._draw();
      ball.bounce_ball(stage, bricks);
      stage.game_over(ball);
      }
    
    // allow user to hold down key for repetitive actions
    void keyPressed()
    {
      if (key == 'a') { keyA = true; }
      if (key == 'd') { keyD = true; }  
      if (key == 'r') { keyR = true; }
    }
    
    void keyReleased(){
      if (key == 'a') { keyA = false; }
      if (key == 'd') { keyD = false; }
      if (key == 'r') { keyR = false; }
    }
    
    
    class Ball {
      float x, y; 
      float w = 30; 
      float h = 30;
      float r;
      float xd = 4;
      float yd = 3.2;
      color c = #FFFFFF;
    
    
      Ball(float x, float y) {
        this.x = x;
        this.y = y;
        this.r = h/2;
      } 
    
    
      void move() {
        // Move the ball by (xd,yd) on the cartesian plane.
        this.x += this.xd;
        this.y += this.yd;
      }
    
      void _draw() {
        fill(c);
        ellipse(cx(x), cy(y), w, h);
      }
    
      void bounce_ball(Stage s, ArrayList<Brick> bs) {
        // Have the ball bounce off bricks and walls.
        h_wall_bounce(s);
        v_wall_bounce(s);
        paddle_bounce(paddle_lower);
        paddle_bounce(paddle_upper);
        for (int i = 0; i < bs.size(); i++) {
          h_brick_wall_bounce(bs.get(i));
          v_brick_wall_bounce(bs.get(i));
        }
      }
    
      void h_wall_bounce(Stage s) {
        // Detect if ball has collided with any horizontal wall. If so, call game_over function.
        boolean hit_ceiling = ball_line(this.x, this.y, this.r, s.ulx, s.uly, s.urx, s.ury);
        boolean hit_floor = ball_line(this.x, this.y, this.r, s.llx, s.lly, s.lrx, s.lry);
        if (stage.game_over_status == false) {
          if (hit_ceiling || hit_floor) {
            stage.game_over_status = true;
          }
        }
      }
    
      void v_wall_bounce(Stage s) {
        // Detect if ball has collided with any vertical wall.
        boolean hit_leftwall = ball_line(this.x, this.y, this.r, s.ulx, s.uly, s.llx, s.lly);
        boolean hit_rightwall = ball_line(this.x, this.y, this.r, s.urx, s.ury, s.lrx, s.lry);
    
        if (hit_leftwall || hit_rightwall) {
          this.xd = this.xd * -1;
        }
      }
    
      void h_brick_wall_bounce(Brick b) {
        if (b.visible) {
          boolean hit_top_wall = ball_line(this.x, this.y, this.r, b.ulx, b.uly, b.urx, b.ury);
          boolean hit_bottom_wall = ball_line(this.x, this.y, this.r, b.llx, b.lly, b.lrx, b.lry);
    
          if (hit_top_wall || hit_bottom_wall) {
            this.yd = this.yd * -1;
            //b.toggle_visible(false);
            b.hit_brick(stage);
          }
        }
      }
    
      void v_brick_wall_bounce(Brick b) {
        if (b.visible) {
          boolean hit_left_wall = ball_line(this.x, this.y, this.r, b.ulx, b.uly, b.llx, b.lly);
          boolean hit_right_wall = ball_line(this.x, this.y, this.r, b.urx, b.ury, b.lrx, b.lry);
    
          if (hit_left_wall || hit_right_wall) {
            this.xd = this.xd * -1;
            //b.toggle_visible(false);
            b.hit_brick(stage);
          }
        }
      }
    
      void paddle_bounce(Paddle p) {
        // Detect if ball has collided with a paddle.
        boolean hit_top_wall = ball_line(this.x, this.y, this.r, p.ulx, p.uly, p.urx, p.ury);
        boolean hit_bottom_wall = ball_line(this.x, this.y, this.r, p.llx, p.lly, p.lrx, p.lry);
        if (hit_top_wall || hit_bottom_wall) {
          this.yd = this.yd * -1;
        }
      }
    }
    
    class Brick {
      float x, y; 
      float w = 100; 
      float h = 50;
      color c;
      float ulx, uly; // corners of the brick. 
      float urx, ury;
      float llx, lly;
      float lrx, lry;
      boolean visible = true;
    
      Brick(float x, float y, color c) {
        this.x = x;
        this.y = y; // x = 0, y = 0  
        this.ulx = x;
        this.uly = y;
        this.urx = x+w;
        this.ury = y;
        this.llx = x;
        this.lly = y-h;
        this.lrx = x+w;
        this.lry = y-h;
        this.c = c;
      } 
    
      void _draw() {
        if (visible) {
          fill(this.c);
          rect(cx(x), cy(y), w, h);
        }
      }
    
      void highlight_corners() {
        // Draw circles on the corners of the brick to help with debugging.
        fill(#FF6C6C);
        ellipse(cx(ulx), cy(uly), 20, 20);
        ellipse(cx(urx), cy(ury), 20, 20);
        ellipse(cx(llx), cy(lly), 20, 20);
        ellipse(cx(lrx), cy(lry), 20, 20);
      }
      
      void toggle_visible(boolean b) {
        // Toggle visibility of brick.
        this.visible = b;
      }
      
      void hit_brick(Stage s){
        toggle_visible(false);
        s.score += 1;
      }
    }

    void create_bricks() {
      // Initialize bricks with different color for each layer.
      for (int i = -6; i < 5; i++) { //creates 7
        bricks.add(new Brick(i*100+50, -50, #ffb3ba)); //Pastel red
        bricks.add(new Brick(i*100+50, 0, #ffdfba)); //Pastel orange
        bricks.add(new Brick(i*100+50, 50, #ffffba)); //Pastel yellow
        bricks.add(new Brick(i*100+50, 100, #baffc9)); //Pastel green
        bricks.add(new Brick(i*100+50, 150, #bae1ff)); //Pastel blue
    
      }
    }
    
    void draw_bricks() {
      for (int i = 0; i < bricks.size(); i++) {
        bricks.get(i)._draw();
      }
    }
    
    void reset_bricks() {
      // Restore visibility for all bricks.
      for (int i = 0; i < bricks.size(); i++) {
        bricks.get(i).toggle_visible(true);
      }
    }
        
    class Paddle {
      float w = 130;
      float h = 10;
      float x, y;
      float ulx, uly; //Corners of the paddle. 
      float urx, ury;
      float llx, lly;
      float lrx, lry;
      float spd = 10;
    
      Paddle(float x, float y, float spd) {
        this.x = x;
        this.y = y;
        this.ulx = x;
        this.uly = y;
        this.urx = x+w;
        this.ury = y;
        this.llx = x;
        this.lly = y-h;
        this.lrx = x+w;
        this.lry = y-h;
        this.spd = spd;
      }
    
      void _draw()
      {
        rect(cx(x), cy(y), w, h);
      }
      void move()
      {
        if (keyA) {
          move_paddle(-this.spd);
        }
        if (keyD) {
          move_paddle(this.spd);
        }
      }
    
      void move_paddle(float n) {
        this.x += n;
        this.ulx += n;
        this.urx += n;
        this.llx += n;
        this.lrx += n;
    
      }
    }
    
    class Stage {
      int score = 0;
      float ulx, uly;
      float urx, ury;
      float llx, lly;
      float lrx, lry;
      float gameoverx = -450; // coordinates for the "Game Over" text.
      float gameovery = -50;
      boolean game_over_status = false;
    
    
      Stage(float sw, float sh) {
        this.ulx = -sw/2;
        this.uly = sh/2;
        this.urx = sw/2;
        this.ury = sh/2;
        this.llx = -sw/2;
        this.lly = -sh/2;
        this.lrx = sw/2;
        this.lry = -sh/2;
      }
    
      void game_over(Ball b) {
        // Draw game over message, restrict movement of paddles, and wait for restart command.
        if (this.game_over_status) {
          draw_game_over_message();
          paddle_upper.spd = 0;
          paddle_lower.spd = 0;
          b.xd = 0;
          b.yd = 0;
          if (keyR) {
            this.restart();
          }
        }
      }
    
      void restart() {
        // Restart the game.
        this.game_over_status = false;
        this.score = 0;
        reset_bricks();
        paddle_lower.spd = 10;
        paddle_upper.spd = -10;
        ball.xd = 4;
        ball.yd = 3.2;
        ball.x = 80;
        ball.y = -300;
      }
    
      void draw_game_over_message() {
        // Draw "Game Over" message. Should be run after draw_bricks() in draw() loop so message appears on top.
        textSize(160);
        fill(#CECCCC);
        text("GAME OVER", cx(gameoverx-2), cy(gameovery-2));
        text("GAME OVER", cx(gameoverx+2), cy(gameovery+2));
        text("GAME OVER", cx(gameoverx-2), cy(gameovery+2));
        text("GAME OVER", cx(gameoverx+2), cy(gameovery-2));
        fill(#FFFFFF);
        text("GAME OVER", cx(gameoverx), cy(gameovery));
      }
    
      void draw_score() {
        // Draw the current score with some transparency.
        textSize(50);
        fill(255, 255, 255, 150);
        text("Score: " + this.score, cx(-screen_width/2 + 50), cy(screen_height/2 - 100));
      }
    }
    
    boolean ball_line(float bx, float by, float r, float lx1, float ly1, float lx2, float ly2) {
      // Circle-line segment collision detection algorithm by bobobobo on StackOverflow: https://stackoverflow.com/a/1084899
      float dx = lx2 - lx1;
      float dy = ly2 - ly1;
      float fx = lx1 - bx;
      float fy = ly1 - by;
      float a = dx*dx + dy*dy;
      float b = 2*(fx*dx + fy*dy);
      float c = (fx*fx + fy*fy) - r*r;
      float discriminant = b*b-4*a*c;
      
      if (discriminant < 0)
        {
          // no intersection
        }
      else
      {
        discriminant = sqrt(discriminant);
        float t1 = (-b - discriminant)/(2*a);
        float t2 = (-b + discriminant)/(2*a);
        
        if (t1 >= 0 && t1 <= 1){
          return true;
        }
        if (t2 >= 0 && t2 <= 1)
        {
          return true;
        }
      }
      return false;
    }
    
    void draw_circle(float x, float y) {
      // Draw a red circle at the specified coordinate.
      fill(#ffb3ba);
      ellipse(cx(x), cy(y), 20, 20);
    }
    
    float cx (float x) {
      //convert processing y coors to cartesian coors
      return (screen_width/2 + x);
    }
      
    float cy(float y){
      //convert processing x coors to cartesian coors
      return (screen_height/2 - y);
    }
    
    float ucx(float x){
      //convert y coors to from cartesian to processing coors
      return (x - screen_width/2);
    }
    
    float ucy(float y){
      //convert y coors from cartesian to processing coors
      return (screen_height/2 - y);
    }
    </script>

    <canvas id="processing-canvas"> </canvas></center>
    <center>
    <p><b>Instructions:</b></p>
    <p>Use the 'a' key to move the paddle left, and the 'd' key to move the paddle right.</p>
    </center>
</body>
</html>
