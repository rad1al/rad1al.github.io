<html>
<head>
    <title>orbits</title>
    <script src="processing-1.4.8.min.js"></script>
</head>
<body>
    <center><h1><font size="20">orbits</font></h1>
    <p>Sketch rendered with Processing.js</p>
    <script type="text/processing" data-processing-target="processing-canvas">
    /**  
    * A Simple Solar System Animation
    * @author Charles (Qiwei) Jin
    * @version 1.0 
    * Copyright 2016
    */ 
    
    float screen_height = 1000;
    float screen_width = 1000;
    
    SolarSystem ssystem = new SolarSystem();
    void setup() {
      size(1000, 1000); // Set window dimensions.
      // pixelDensity(2); // Turn on retina mode for retina screens. Doesn't work with Processing.js
    }
    
    void draw() {
      //background(#000000);
      //fill(#FFF755);
      //ellipse(cx(0), cy(0), 50, 50);
      ssystem.run();
    }
    
    /**
     * The Moon class keeps track of the dimension, coordinates 
     * and color of a moon. It has methods for moving and displaying.
     */
    
    class Moon {
      float orbital_distance;
      float w;
      float theta = 0;
      float rx;
      float ry;
      float x; // x coordinate in solar system
      float y; // y coordinate in solar system
      color c = #FFFFFF;
      
      Moon(float od, float w) {
        orbital_distance = od;
        this.w = w;
      }
    
      void orbit_planet(Planet p, float v) {
        // Orbit moon around planet with velocity v
        theta = theta + v;
        rx = cos(theta); // Use unit circle representation to represent position and angle.
        ry = sin(theta);
        x = orbital_distance*rx + p.orbital_distance*p.x;
        y = orbital_distance*ry + p.orbital_distance*p.y;
      }
    
      void display() {
        // Draw moon.
        fill(c);
        ellipse(cx(x), cy(y), w, w);
      }
      
    
    }
    
    /**
     * The Planet class keeps track of the dimension, coordinates 
     * and color of a planet. It has methods for moving and displaying.
     */
    
    class Planet {
      float orbital_distance;
      float w;
      float theta = 0;
      float x;
      float y;
      float v; //orbital velocity
      color c;
    
      Planet(float od, float w, float v, color c) {
        orbital_distance = od;
        this.w = w;
        this.v = v;
        this.c = c;
      }
    
      void orbit() {
        // Orbit planet around the sun with velocity v
        theta = theta + v;
        x = cos(theta);
        y = sin(theta);
      }
    
      void display() {
        // Draw planet.
        fill(this.c);
        ellipse(cx(orbital_distance*x), cy(orbital_distance*y), w, w);
      }
    }
    
    /**
     * The SolarSystem class keeps track of all the objects in the system
     * and has methods for moving and displaying them.
     */
    class SolarSystem {
      Sun sun = new Sun(50);  
      Moon moon1 = new Moon(40, 15);
      Moon moon2 = new Moon(50, 10);
      float[] planet_orbital_dists = {100, 150, 225, 400};
      float[] planet_widths = {10, 20, 30, 20};
      float[] planet_velocities = {0.09, 0.05, 0.04, 0.02};
      color[] planet_colors = {#AA4C39, #D4A26A, #6D92A0, #7BB992};
      ArrayList <Planet> planets = new ArrayList();
    
      SolarSystem() {
        // Initialize all the planet objects and store them in an Arraylist. 
        for (int i = 0; i < planet_widths.length; i++) {
          this.planets.add(new Planet(planet_orbital_dists[i], planet_widths[i], planet_velocities[i], planet_colors[i]));
        }
      }
    
      void run() {
        // Animate the solar system.
        background(#000000);
        sun.display();
        move_and_display_planets();
        move_and_draw_moons();
      }
    
      void move_and_display_planets() {
        // Move and draw the planets in the solar system.
        for (int i = 0; i < planet_widths.length; i++) {
          //Planet p = Planet(planet_widths[i], planet_orbital_dists[i])
          this.planets.get(i).display();
          this.planets.get(i).orbit();
        }
      }
    
      void move_and_draw_moons() {
        // Move and draw the moons in the solar system.
        moon1.orbit_planet(this.planets.get(2), 0.1);
        moon1.display();
        moon2.orbit_planet(this.planets.get(3), 0.05);
        moon2.display();
      }
    }
    
    /**
     * The Sun class represents the star in the center of the system 
     */
    class Sun {
      float radius; // The radius of the sun.
      float x = 0; // The x coordinate.
      float y = 0; // The y coordinate.
      color c = #FFF755; // Yellow in hex representation.
    
      Sun(float radius) {
        this.radius = radius;
      }
    
      void display() {
        fill(this.c); // Color the sun yellow.
        noStroke(); // Indicate drawing will not have a border.
        ellipse(cx(0), cy(0), this.radius, this.radius); // Draw a yellow circle at (x,y).
      }
    } 
    
    float cx (float x) {
      //convert processing y coors to cartesian coors
      return (screen_width/2 + x);
    }
      
    float cy(float y){
      //convert processing x coors to cartesian coors
      return (screen_height/2 - y);
    }
    
    float ucx(float x){
      //convert y coors to from cartesian to processing coors
      return (x - screen_width/2);
    }
    
    float ucy(float y){
      //convert y coors from cartesian to processing coors
      return (screen_height/2 - y);
    }
    </script>
    <canvas id="processing-canvas"> </canvas></center>
</body>
</html>
